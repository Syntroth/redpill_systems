################################
### PARENT: Shuttle Kiosk (#203)
################################

# Setup the MUSH object
@set #203=!SAFE NO_COMMAND
-
@wipe #203
-
@set #203=SAFE
-
@desc #203=
  A kiosk for purchasing shuttle tickets.%r%r
  COMMANDS:%r
  [ansi(yh,shuttle/claim <ship name>)] - Claim a one-time-only free shuttle.%r
  [ansi(yh,shuttle/ride <number>)] - Take a free taxi to your destination.%r%r
  Travel Times:%r
  [iter(
      remove(v(list.destinations),loc(%!)),
      <#@> [ljust(name(zone(itext(0))),10)] [timestring(u(fn.travel.calc,loc(%!),itext(0)))],
      ,%r
      )]%r
  -
-
&DATA #203=#351
-
&CMD.SHUTTLE/CLAIM #203=$shuttle/claim *:
  @switch [not(spacesys(hasshuttle,%#))]=

          0,@nspemit %#=[ansi(rh,>)] You have already claimed your one free shuttle.,
          1,{@nspemit %#=[ansi(gh,>)] You have claimed your one-time-only free shuttle.  Name: [setr(0,[capstr(%0)])];
              think [setr(1,spacesys(clone.ship,get(v(data)/parent.shuttle),%q0))];
              @nsremit [loc(%#)]=Maintenance crews direct the %q0 as it's towed in from a nearby shipyard.;
              @tel %q1=[loc(%#)];
              @set %q1=data.roster:%#:captain;
              @set [v(data)]=list.shuttles.players:[trim([get(v(data)/list.shuttles.players)] %#:%q1)];
              @set %#=space`shuttle:%q1;
              @set %q1=enter_ok;
              @set %q1=space`report:;
              @set %q1=space`destination:;
              @set %q1=data.docked:[loc(%#)];
              think [syslog(SPACE,[name(%#)]\(%#\) claimed a free shuttle %q1 at [name(loc(%#))]\([loc(%#)]\))]
             }
-
&CMD.DEPART #580=$depart:
  @switch [lte(secs(),last(setr(0,extract(v(passengers),match(v(passengers),%#:*))),:))]=

          1,@nspemit %#=[ansi(rh,>)] You will arrive at your destination in [ansi(yh,timestring(sub(last(%q0,:),secs())))].,
          0,{@nsoemit %#=[name(%#)] disembarks from the shuttle at [ansi(wh,extract(%q0,2,1,:))].;
             @set %!=passengers:[remove(v(passengers),%q0)];
             @tel %#=[extract(%q0,2,1,:)]
            }
-
&CMD.SHUTTLE/RIDE #203=$shuttle/ride *:
  @switch [t(lte(%0,words(setr(3,remove(v(list.destinations),loc(%!))))))]=

          0,@nspemit %#=[ansi(rh,>)] That is not a valid destination.,
          1,{@nspemit %#=[ansi(gh,>)] You purchase your ticket to [name(setr(1,extract(zones(lzone,setr(0,extract(%q3,%0,1))),2,1)))].;
             @nsoemit %#=[name(%#)] purchases a shuttle ticket and boards a transport from the platform.;
             @tel %#=[v(shuttle)];
             @nsoemit %#=[name(%#)] boards the shuttle from [name(%q1)].;
             @nspemit %#=[ansi(gh,>)] You will arrive at your destination in [ansi(yh,timestring(setr(2,u(fn.travel.calc,loc(%!),%q0))))].;
             @set [v(shuttle)]=passengers:[trim([get(v(shuttle)/passengers)] %#:%q0:[add(secs(),%q2)])];
             @wait [v(shuttle)]/%q2={@nspemit %#=You have arrived at your destination and may now [ansi(yh,depart)].}
            }
-
&FN.TRAVEL.CALC #203=
  mul(abs(mul(v(shuttle.multiplier),sub(member(v(list.destinations),%0),member(v(list.destinations),%1)))),60)  
-
&LIST.DESTINATIONS #203=#223 #428 #387 #349 
-
&SHUTTLE.MULTIPLIER #203=10
-
&SHUTTLE #203=#580
-
