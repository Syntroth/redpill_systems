# on player connect, trigger everything that's waiting...
&player`connect #13 =
  /@@ %0 = objid, %1 = number of connections, %2 = descriptor @@/
  @trigger #114/trig.aconnect=%0;
  @trigger #227/trig.aconnect=%0;
  @trigger #812/trig/aconnect=%0;
  think [if(hasattr(%0/pfile),rpc(PlayerFile.connect,get(%0/pfile),get(%0/lastip),get(%0/lastsite),%2,num(%0)))];
  @swi [haspower(%0,guest)]=1,@tri #459/tri.guest.connect=%0;
  @swi [or(haspower(%0,guest),not(hasattr(%0,char`announced)))]=1,{
    @wait 7=@nspemit %#=[ansi(h,New to MUSHes?)] You can talk to others by typing: [ansi(h,+help Hi there!)]%rWe're friendly - feel free to ask questions, or ask for help! Your name is [ansi(h,name(%#))].
  };
  @swi [nor(hidden(%0),hasflag(%0,dark),hasattr(%0,char`announced))]=
    1,{
      &char`announced %0=1;
      @cemit/noisy help=[ansi(c,Say hello to our newest player\,)] [ansi(h,name(%0))][ansi(c,!)]
    }

-

&player`disconnect #13 =
  /@@ %0 = objid, %1 = number of remaining connections, %2 = hidden?, %3 = cause of disconnection, %4 = ip, %5 = descriptor, %6 = conn() secs, %7 = idle() secs, %8 = recv bytes/sent bytes/command/count @@/
  think [if(hasattr(%0/pfile),rpc(PlayerFile.disconnect,get(%0/pfile),%5))];
  @swi [haspower(%0,guest)]=1,@tri #459/tri.guest.disconnect=%0
-

&player`create #13 =
  /@@ %0 = objid, %1 = name, %2 = how, %3 = descriptor @@/
  @swi %2=
  pcreate,
  {
    think [syslog(
      PCREATE,
      New player [name(%0)]\([num(%0)]\) has been @pcreated by [name(%#)].
    )]
  },
  {
    think [syslog(
      PCREATE,
      New player [name(%0)]\([num(%0)]\) has been %2ed to [get(%0/registered_email)].
    )];
    @assert [hasattr(%0,registered_email)];
    +pfile/register [get(%0/registered_email)]=[num(%0)];
  };
  ;@chan/on help=%0

