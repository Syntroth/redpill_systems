##################################
### PARENT: Command Console (#204)
##################################

# Setup the MUSH object
@set #204=!SAFE NO_COMMAND
-
@wipe #204
-
@set #204=SAFE
-
@lock/use #204=LOCK.CANUSECONSOLE/1
-
@lock/command #204=LOCK.ISMANNING/1
-
@lset #204/use=!no_inherit
-
@lset #204/command=!no_inherit
-
@ufail #204=[pemit(%#,
  [if([spacesys(ismanned,%!)],
    /@@ Console is manned by someone else @@/
    The [name(%!)] is already being manned by [spacesys(mannedby,%!)].,
    
    /@@ Access denied @@/
    Console access denied.
  )]
)]
-
@desc #204=A command 
  /@@ Command-highlight 'console' to whoever is manning the console @@/
  [if([spacesys(ismanning,%!,%#)],
    [spacesys(hicmd,console)],
    /@@ Otherwise, show the plain word @@/
    console
  )]. It is currently [switch([v(data.manned)],0,unmanned.,manned by [spacesys(mannedby,%!)].)]
-
&LOCK.CANUSECONSOLE #204=[spacesys(canuseconsole,%!,%#)]
-
&LOCK.ISMANNING #204=[spacesys(ismanning,%!,%#)]
-
&AUSE #204=
  @name %!=[trim(before(name(%!),<))] <[name(%#)]>;
  &data.manned %!=%#
-
&CMD.UNMAN #204=$unman:
  @name %!=[trim(before(name(%!),<))] <Unmanned>;
  @pemit %#=You unman the [trim(before(name(%!),<))];
  @oemit %#=[name(%#)] unmans the [trim(before(name(%!),<))].;
  &data.manned %!;
-
&OUSE #204=
  [if(
      spacesys(isunmanned,%!),
      [name(%#)] mans the [trim(before(name(%!),<))].
      )]
-
&USE #204=
  [ifelse(
      spacesys(ismanned,%!),
      You are already manning the [trim(before(name(%!),<))].,
      You man the [trim(before(name(%!),<))].
      )]
-
# Command: console
# Diplay the console commands and their syntax
&CMD.CONSOLE #204=$console:[pemit(%#,
  [spacesys(console)]
)]
-
# Command: plot
# Display the plot commands and their syntax
&CMD.PLOT #204=$plot:[pemit(%#,
  [spacesys(plot)]
)]
-
# Command: roster
# Display the roster commands and their syntax
&CMD.ROSTER #204=$roster:[pemit(%#,
  [spacesys(roster)]
)]
-

