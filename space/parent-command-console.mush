##################################
### PARENT: Command Console (#204)
##################################

# Setup the MUSH object
@set #204=!SAFE NO_COMMAND
-
@wipe #204
-
@set #204=SAFE
-
@lock/use #204=LOCK.CANUSECONSOLE/1
-
@lock/command #204=LOCK.ISMANNING/1
-
@lset #204/use=!no_inherit
-
@lset #204/command=!no_inherit
-
&COMMAND_LOCK`FAILURE #204=
  [ifelse(
    not(hasattrval(%!,data.ship)),
    [ansi(rh,WARNING:)] [trim(before(name(%!),<))] does not have a valid ship object set.,
    Huh? \(Type "help" for help.\)
    )]
-
&CMD.SCAN #204=$scan:
  @switch [not(spacesys(isdocked,setr(0,v(data.ship))))]=

          0,@nspemit %#=[ansi(gh,>)] The ship is currently landed.,
          1,{@nspemit %#=[titlebar(Sensor Report: [name(%q0)])]%r
                         %b[ansi(y,align(2 39 21 >12,ID,Name,Class,Distance))]%r
                         %b[ansi(r,--%b[repeat(-,39)]%b[repeat(-,21)]%b[repeat(-,12)])]%r
                         [iter(
                            setr(1,spacesys(list.report,%q0)),
                            %b[align(
                                2 39 21 >12,
                                #@,
                                [name(itext(0))],
                                [spacesys(class,itext(0))],
                                [round(spacesys(distance,%q0,itext(0)),2,1)]km
                                )],
                            ,%r)]%r
                         [footerbar([words(%q1)] Contacts)];
             @set %q0=space`report:[secs()]|%q1
            }
-
@ufail #204=[pemit(%#,
  [if([spacesys(ismanned,%!)],
    /@@ Console is manned by someone else @@/
    The [name(%!)] is already being manned by [spacesys(mannedby,%!)].,
    
    /@@ Access denied @@/
    Console access denied.
  )]
)]
-
@desc #204=A command 
  /@@ Command-highlight 'console' to whoever is manning the console @@/
  [if([spacesys(ismanning,%!,%#)],
    [spacesys(hicmd,console)],
    /@@ Otherwise, show the plain word @@/
    console
  )]. It is currently [switch([v(data.manned)],0,unmanned.,manned by [spacesys(mannedby,%!)].)]
-
&LOCK.CANUSECONSOLE #204=[spacesys(canuseconsole,%!,%#)]
-
&LOCK.ISMANNING #204=[and(spacesys(ismanning,%!,%#),hasattrval(%!,data.ship))]
-
&AUSE #204=
  @name %!=[trim(before(name(%!),<))] <[name(%#)]>;
  &data.manned %!=%#
-
&CMD.UNMAN #204=$unman:
  @name %!=[trim(before(name(%!),<))] <Unmanned>;
  @nspemit %#=You unman the [trim(before(name(%!),<))];
  @nsoemit %#=[name(%#)] unmans the [trim(before(name(%!),<))].;
  &data.manned %!;
-
&OUSE #204=
  [if(
      spacesys(isunmanned,%!),
      [name(%#)] mans the [trim(before(name(%!),<))].
      )]
-
&USE #204=
  [ifelse(
      spacesys(ismanned,%!),
      You are already manning the [trim(before(name(%!),<))].,
      You man the [trim(before(name(%!),<))].
      )] 
-
# Command: console
# Diplay the console commands and their syntax
&CMD.CONSOLE #204=$console:[pemit(%#,
  [spacesys(console)]
)]
-
# Command: plot
# Display the plot commands and their syntax
&CMD.PLOT #204=$plot:[pemit(%#,
  [spacesys(plot)]
)]
-
# Command: roster
# Display the roster commands and their syntax
&CMD.ROSTER #204=$roster:[pemit(%#,
  [spacesys(roster)]
)]
-

