##################################
### PARENT: Command Console (#204)
##################################

# Setup the MUSH object
@set #204=!SAFE NO_COMMAND
-
@wipe #204
-
@set #204=SAFE
-
@lock/use #204=LOCK.CANUSECONSOLE/1
-
@lock/command #204=LOCK.ISMANNING/1
-
@lset #204/use=!no_inherit
-
@lset #204/command=!no_inherit
-
@ufail #204=[pemit(%#,
  [if([spacesys(ismanned,%!)],
    /@@ Console is manned by someone else @@/
    The [name(%!)] is already being manned by [spacesys(mannedby,%!)].,
    
    /@@ Access denied @@/
    Console access denied.
  )]
)]
-
@desc #204=A command 
  /@@ Command-highlight 'console' to whoever is manning the console @@/
  [if([spacesys(ismanning,%!,%#)],
    [spacesys(hicmd,console)],
    /@@ Otherwise, show the plain word @@/
    console
  )]. It is currently [switch([v(data.manned)],0,unmanned.,manned by [spacesys(mannedby,%!)].)]
-
&LOCK.CANUSECONSOLE #204=[spacesys(canuseconsole,%!,%#)]
-
&LOCK.ISMANNING #204=[spacesys(ismanning,%!,%#)]
-
&USE #204=[pemit(%#,
  [if([spacesys(ismanned,%!)],
    /@@ Player is already manning the console. @@/
    You are already manning the [name(%!)].,

    /@@ Set the player's DBREF as manning the console @@/
    [attrib_set(%!/data.manned,%#)]
    You man the [name(%!)].
  )]
)]
-
# Command: console
# Diplay the console commands and their syntax
&CMD.CONSOLE #204=$console:[pemit(%#,
  [spacesys(console)]
)]
-
# Command: plot
# Display the plot commands and their syntax
&CMD.PLOT #204=$plot:[pemit(%#,
  [spacesys(plot)]
)]
-
# Command: roster
# Display the roster commands and their syntax
&CMD.ROSTER #204=$roster:[pemit(%#,
  [spacesys(roster)]
)]
-
# Command: unman
# Unmans the player from the console.
&CMD.UNMAN #204=$unman:[pemit(%#,
  [attrib_set(%!/data.manned,0)]
  You unman the [name(%!)].
)]
-
